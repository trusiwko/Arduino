# Работа с сервоприводом

У серовопривода три провода: коричневый, красный и оранжевый. Коричневый подключаем к земле, красный к питанию, а оранжевый - управляющий пин, который подключается к ШИМ ардуино.

## Tinkercad

Создадим проект на tinkercad, затем перенесем его на ардуино. 
После регистрации/авторизации на сайте https://www.tinkercad.com, на главной странице в меню слева выбираем Circuits, далее жмем кнопку Create new Circuit. Справа расположена панель с компонентами. Компоненты разделены по группам, их также можно фильтровать. Выберем группу Arduino, в которой расположено 22 примера, которые сделаны на основе базовых примеров арудино:

![img1](https://github.com/trusiwko/Arduino/raw/master/eKids/Lesson6/src/img1.png)

Найдем среди них пример Servo и перетащим на страницу:

![img2](https://github.com/trusiwko/Arduino/raw/master/eKids/Lesson6/src/img2.png)

Далее нажмем кнопку Code, после чего откроется Scratch-подобный интерфейс для разработки, сменим его на привычный нам текст - меняем в списке Blocks на Text и подтверждаем выбор кнопкой Continue (здесь написано, что вернуться обратно в стиль "Блоки" не получится).

Для запуска скетча нужно нажать на кнопку Start Simulation.

## Код скетча

Для работы с сервоприводом существует библиотека Servo.h. Для подключения библиотеки в ардуино используется команда `#include`. Таким образом, срока
```C++
#include <Servo.h>
```
подключает библиотеку. Теперь мы можем в коде использовать объект из данной библиотеки. Следующая строка
```C++
Servo servo_9;
```
объявляет переменную с именем `servo_9` и "типом" `Servo`, который ардуино теперь знает благодаря подключенной библиотеке. Теперь мы можем использовать разработанные методы данного типа, вызывая их у этой переменной. Мы будем использовать два метода:
```C++
servo_9.attach(pin);
servo_9.write(pos);
```
Метод `attach` указывает к какому пину ардуино подключен контрольный пин сервопривода. На нашем скетче это 9й пин. Данный метод вызывается один раз в блоке _setup_.
Метод `write` указывает на какой градус повернуть сервопривод. Сервопривод принимает значения от 0 до 180, т.е. сервопривод можно повернуть только на угол от 0 до 180.

В коде предложенного скетча два цикла - первый от 0 до 180 с шагом 1 и второй обратный от 180 до 0 (с шагом -1). В каждом из циклов мы позиционируем сервопривод на указанную позицию и ждем 15 миллисекунд. Сервопривод двигается от 0 до 180 и от 180 до 0 и цикл повторяется. 

Полный код скетча:
```C++
#include <Servo.h> // Подключаем библиотеку для работы с сервоприводом 
int pos = 0;
Servo servo_9; // Переменная для управления сервоприводом
void setup()
{
  servo_9.attach(9); // Говорим к какому пину присоединен сервопривод
}

void loop()
{
  for (pos = 0; pos <= 180; pos += 1) {
    servo_9.write(pos); // Отправляем на сервопривод текущую позицию
    delay(15);
  }
  for (pos = 180; pos >= 0; pos -= 1) {
    servo_9.write(pos);
    delay(15);
  }
}
```
![demo](https://github.com/trusiwko/Arduino/raw/master/eKids/Lesson6/src/demo1.gif)

## Управление сервоприводом с помощью потенциометра

Добавим потенциометр для управления сервоприводом, среднюю ногу которого подключим к пину А0 ардуино, а крайние две ноги на питание и землю. 
> На tinkercad сбросим выбранный ранее фильтр Arduino и выберем Basic (основной). Для составления скетча нам понадобится бредборд (Breadboard Small) и потенциометр (Potentiometer). Слова указанные в скобках можно писать в фильтре, чтобы долго не искать по списку.

Вспомним, что на аналоговом пине (с помощью функции `analogRead`) ардуины мы получаем значение от 0 до 1023, а сервоприводу нужно передавать от 0 до 180. Мы можем считать показания потенциометра и разделить на 5.7. Таким образом получим нужные показания, но это не особо удобно. В ардуино существует функция, которая преобразовывает диапазоны из одного в другой, т.е. она может преобразовать числа от 0 до 1023 в числа от 0 до 180. Это функция `map`:
```C++
pos = map(p, 0, 1023, 0, 180);
```
Здесь пять параметров. Первый - число, которое мы преобразовываем, далее два параметра - границы, в которых находится данное число - от 0 до 1023. Последние два параметра - границы, в которых должно находиться новое число - от 0 до 180. 
В итоге если число p будет содержать 0, то и в результате функции мы получим 0, если там будет 1023, то в результате функции мы получим 180. Если будет 512 (половина от 1023), то получим около 90 (половина от 180). 

Для считывания показаний с пина А0 добавим еще одну переменную `p` типа `int`. 
В блоке _setup_ не забудем указать `pinMode(A0, INPUT)`, а блок _loop_ перепишем полностью:
1) Считаем показания с пина А0 (потенциометр).
2) Преобразуем показания с диапазона 0..1023 в диапазон 0..180.
3) Передадим указанное значение на сервопривод (пин 9).
4) Подождем 50 миллисекунд.

Получим код:
```C++
#include <Servo.h>

int pos = 0;
int p; // Переменная для считывания показаний с потенциометра

Servo servo_9;

void setup()
{
  servo_9.attach(9);
  pinMode(A0, INPUT);
}

void loop()
{
  p = analogRead(A0); // Считываем показания с потенциометра (вернется число от 0 до 1023)
  pos = map(p, 0, 1023, 0, 180); // Преобразуем число из диапазона 0..1023 в число из диапазона 0..180
  servo_9.write(pos); // Передаем полученное число в сервопривод
  delay(50);
}
```
![demo](https://github.com/trusiwko/Arduino/raw/master/eKids/Lesson6/src/demo2.gif)

## Добавим светодиод

Добавим красный светодиод, который будет загораться когда мы приближаемся к крайним значениям. 
> На tinkercad светодиод называется LED, длинная нога светодиода показана изогнутой (и расположена справа). Обратите внимание, на скетче я развернул светодиод на 180 градусов вниз головой и длинная нога оказалась слева.

Когда до крайней точки останется менее 50, будем зажигать светодиод, иначе будем его тушить. Таким образом проверяем число на < 50 или > 973 (это 1023 - 50).

Светодиод подключим короткой ногой на землю, длинной через резистор 220 Ом на пин 10.

Не забываем добавить в блок _setup_ команду:
```C++
pinMode(10, OUTPUT);
```
а в блоке _loop_ добавляем условие:
```C++
  if (p > 973 or p < 50) {
    digitalWrite(10, HIGH);
  } else {
    digitalWrite(10, LOW);
  }
```
Получим код:
```C++
#include <Servo.h>

int pos = 0;
int p;

Servo servo_9;

void setup()
{
  servo_9.attach(9);
  pinMode(A0, INPUT);
  pinMode(10, OUTPUT);
}

void loop()
{
  p = analogRead(A0);
  pos = map(p, 0, 1023, 0, 180);
  servo_9.write(pos);
  if (p > 973 or p < 50) {
    digitalWrite(10, HIGH);
  } else {
    digitalWrite(10, LOW);
  }
  delay(100);
}
```
![demo](https://github.com/trusiwko/Arduino/raw/master/eKids/Lesson6/src/demo3.gif)
